import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id "scala"
    id "java-library"
    id "jacoco"
    id "com.vanniktech.maven.publish" version "0.29.0"
    id "net.nemerosa.versioning" version "3.1.0"
    // Scala support
    id "com.github.maiflai.scalatest" version "0.32"
    id "cz.augi.gradle.scalafmt" version "1.21.3"
    id "io.github.cosmicsilence.scalafix" version "0.2.2"
}

group = "io.github.mtsongithub.doetl"
// TODO: crossbuild for Scala 2.13
archivesBaseName = "spark-dialect-extension_2.12"
version = "0.0.1"
description = "Spark dialect extension for enhanced type handling"

repositories {
    mavenCentral()
}

dependencies {
    // Scala support
    implementation "org.scala-lang:scala-library:2.12.19"
    // core Spark dependencies
    // TODO: crossbuild for Spark 3.2-3.5
    implementation "org.apache.spark:spark-core_2.12:3.5.1"
    implementation "org.apache.spark:spark-sql_2.12:3.5.1"
    // latest compatible with Java 8
    implementation "ch.qos.logback:logback-classic:1.3.14"

    // test dependencies
    testImplementation "org.scalatest:scalatest_2.12:3.2.19"
    testImplementation "com.vladsch.flexmark:flexmark-all:0.64.8"
    testImplementation "org.mockito:mockito-scala_2.12:1.17.37"
    // latest compatible with Java 8
    testImplementation "io.github.cdimascio:dotenv-java:2.3.2"

    // Clickhouse dependency
    testImplementation "com.clickhouse:clickhouse-jdbc:0.6.0-patch5"
    testImplementation "org.apache.httpcomponents.client5:httpclient5:5.3.1"
    testImplementation "org.apache.httpcomponents.core5:httpcore5:5.1.5"
}

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
    }
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = ["-Ywarn-unused"]
}

getTasksByName("test", true).each { task ->
    task.dependsOn(jar)
}

task printVersion {
    doLast {
        println project.version
    }
}

task scaladocJar(type: Jar, dependsOn: scaladoc) {
    archiveClassifier = "scaladoc"
    from scaladoc.destinationDir
}

java {
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

jar {  
    manifest {
        attributes(
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor": "MTS PJSC",
            "Created-By": "Gradle ${gradle.gradleVersion}",
            "Built-By": System.properties["user.name"],
            "Build-Timestamp": versioning.info.time,
            "Build-Revision": versioning.info.commit,
            "Build-Jdk": "${System.properties["java.version"]} (${System.properties["java.vendor"]} ${System.properties["java.vm.version"]})",
            "Build-OS": "${System.properties["os.name"]} ${System.properties["os.arch"]} ${System.properties["os.version"]}",
        )
    }
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()

    coordinates(project.group, "spark-dialect-extension_2.12", project.version)

    pom {
        name = "spark-dialect-extension"
        url = "https://theplatform.ru/platforms/dataops"
        inceptionYear = "2024"
        licenses {
            license {
                name = "Apache-2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "doetl"
                name = "DataOps.ETL"
                email = "onetools@mts.ru"
            }
        }
        scm {
            connection = "scm:git:git://github.com/MobileTeleSystems/spark-dialect-extension.git"
            developerConnection = "scm:git:ssh://github.com/MobileTeleSystems/spark-dialect-extension.git"
            url = "https://github.com/MobileTeleSystems/spark-dialect-extension"
        }
    }
}
